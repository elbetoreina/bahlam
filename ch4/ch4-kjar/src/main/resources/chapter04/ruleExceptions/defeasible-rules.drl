package chapter04.ruleExceptions

import com.bahlam.brms.training.model.Order;
import com.bahlam.brms.training.types.IsLargeOrder;

rule "large orders" @Defeasible
    when Order($id: orderId, total > 150.00)
    then insertLogical(new IsLargeOrder($id));
end

rule "large orders exception" @Defeats( "large orders" ) 
    when Order($id: orderId, total > 150.00, totalItems < 5)
    then insertLogical(new IsLargeOrder($id), "neg");
end

rule "large orders double exception"
  @Defeats( "large orders exception" )
    when Order($id: orderId, total > 300.00)
    then insertLogical(new IsLargeOrder($id));
end

rule "print large orders after processing"
    salience -10
    when $o: IsLargeOrder()
    then System.out.println($o);
end

